# Maintainer: Pat Brisbin <pbrisbin@gmail.com>
pkgname='gitsh'
pkgver=0.11.2
pkgrel=1
pkgdesc='An interactive shell for git'
arch=('any')
url="http://thoughtbot.github.io/gitsh/"
license=('custom')
depends=('ruby>=2.0.0' 'readline')
source=("http://thoughtbot.github.io/gitsh/gitsh-0.11.2.tar.gz")
sha256sums=('d3ef8f88417ff9bef7fd95bcd1d9011d9adaed2213094a2d056266169da1bcec')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
