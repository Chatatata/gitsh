# Maintainer: Pat Brisbin <pbrisbin@gmail.com>
pkgname='gitsh'
pkgver=0.9
pkgrel=1
pkgdesc='An interactive shell for git'
arch=('any')
url="http://thoughtbot.github.io/gitsh/"
license=('custom')
depends=('ruby>=1.9.3')
source=("http://thoughtbot.github.io/gitsh/gitsh-0.9.tar.gz")
sha1sums=('d6920cfbb7f2f974b38b4478105c837b356345a5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
