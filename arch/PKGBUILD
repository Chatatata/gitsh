# Maintainer: Pat Brisbin <pbrisbin@gmail.com>
pkgname='gitsh'
pkgver=0.11.1
pkgrel=1
pkgdesc='An interactive shell for git'
arch=('any')
url="http://thoughtbot.github.io/gitsh/"
license=('custom')
depends=('ruby>=2.0.0' 'readline')
source=("http://thoughtbot.github.io/gitsh/gitsh-0.11.1.tar.gz")
sha256sums=('a3e868d140e9972b20c022ff43c761d2643026b10e2c6f67f0ccf47f891a4d6f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
